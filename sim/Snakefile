# Snakefile

rule all:
    input:
        "output/allele_freq_matrix.csv",
        "output/simulated_sequences/recombinants.csv",
        "output/inferred/inferred_lineages.json",
        "output/inferred/inferred_lineages_controls.json",
        "output/results/breakpoint_distance_by_count.png"

        # "output/inferred/inferred_lineages.json",
        # "output/inferred/optimization_results.csv",
        # "output/inferred/inferred_lineages_controls.json",
        # "output/inferred/optimization_results_controls.csv",
        # "output/analysis/breakpoint_distances.png",
        # "output/analysis/correct_vs_hamming.png",
        # "output/analysis/first_20_true_and_inferred.png",

rule filter_England_data:
    message: "Filtering the sequences from England between September 2020 and March 2023 from all SARS-CoV-2 sequences."
    input:
        fasta="../data/aligned.fasta.zst", 
        metadata="../data/metadata.tsv.zst"
    output:
        output_path="output/aligned.min-date.2020-09-01.max-date.2023-03-31.division.England.fasta",
        output_metadata_path="output/aligned.min-date.2020-09-01.max-date.2023-03-31.division.England.metadata.tsv"
    conda: "../envs/nextstrain.yaml"
    shell:
        """
        augur filter \
            --metadata {input.metadata} \
            --sequences {input.fasta} \
            --min-date 2020-09-01 \
            --max-date 2023-03-31 \
            --query "division == 'England'" \
            --output {output.output_path} \
            --output-metadata {output.output_metadata_path}
        """

rule get_reference_metadata:
    message: "Obtaining reference IDs and dates from England data between 2022-11-19 and 2022-12-18."
    input:
        metadata="output/aligned.min-date.2020-09-01.max-date.2023-03-31.division.England.metadata.tsv"
    output:
        output_metadata="output/sliding_windows/ref_2022-11-19_2022-12-18.metadata.tsv",
        output_id="output/sliding_windows/ref_2022-11-19_2022-12-18.txt"
    shell:
        """
        tsv-select -f 1,7,10,35 {input.metadata} | \
            tsv-filter --header --str-ge 2:2022-11-19 --str-le 2:2022-12-18 > {output.output_metadata} && \
        tsv-select -f 1 {output.output_metadata} | tsv-sample -n 100000 --seed-value 26 > {output.output_id}
        """

rule get_reference_fasta:
    message: "Obtaining reference sequences from England data between 2022-11-19 and 2022-12-18."
    input:
        fasta="output/aligned.min-date.2020-09-01.max-date.2023-03-31.division.England.fasta",
        ids="output/sliding_windows/ref_2022-11-19_2022-12-18.txt"
    output:
        output_path="output/sliding_windows/ref_2022-11-19_2022-12-18.fasta.gz"
    shell:
        """
        seqkit grep -f {input.ids} {input.fasta} | gzip > {output.output_path}
        """

rule collapse_lineages:
    message: "Collapsing Pango lineages for ref_2022-11-19_2022-12-18."
    input:
        fasta="output/sliding_windows/ref_2022-11-19_2022-12-18.fasta.gz",
        metadata="output/sliding_windows/ref_2022-11-19_2022-12-18.metadata.tsv"
    output:
        output_path="output/sliding_windows/seq_pango_sorted_2022-11-19_2022-12-18.csv.gz"
    shell:
        """
        python3 ../code/collapse_lineages.py --fasta {input.fasta} --metadata {input.metadata} --trim 0 --collapse_threshold 300 \
            --out {output.output_path}
        """

rule get_allele_freq:
    message: "Obtaining matrix of allele frequencies from the reference set."
    input:
        data="output/sliding_windows/seq_pango_sorted_2022-11-19_2022-12-18.csv.gz"
    output:
        output_path="output/allele_freq_matrix.csv"
    shell:
        """
        python3 ../code/get_allele_freq.py --csv {input.data} --out {output.output_path}
        """

rule sim_sequences:
    message: "Simulating recombinant and control sequences."
    input:
        data="output/sliding_windows/seq_pango_sorted_2022-11-19_2022-12-18.csv.gz"
    output:
        "output/simulated_sequences/recombinants.csv",
        "output/simulated_sequences/sampled_sequences.csv",
        "output/simulated_sequences/breakpoints.csv",
        "output/simulated_sequences/controls.csv",
        "output/simulated_sequences/sampled_sequences_control.csv"
    shell:
        """
        python3 sim_sequences.py --csv {input.data} --rate 0.0002 --n_single 100 --n_double 100 --n_control 800 --no-plot
        """

rule run_HMM:
    message: "Running the hidden Markov model to infer the parental lineages for the simulated recombinant sequences."
    input:
        freq="output/allele_freq_matrix.csv",
        test="output/simulated_sequences/recombinants.csv"
    output:
        out="output/inferred/inferred_lineages.json",
        optim_out="output/inferred/optimization_results.csv"
    shell:
        """
        python3 ../code/run_HMM.py --freq {input.freq} --test {input.test} --out {output.out} --optim_out {output.optim_out}
        """

rule run_HMM_controls:
    message: "Running the hidden Markov model to infer the parental lineages for the simulated control sequences."
    input:
        freq="output/allele_freq_matrix.csv",
        test="output/simulated_sequences/controls.csv"
    output:
        out="output/inferred/inferred_lineages_controls.json",
        optim_out="output/inferred/optimization_results_controls.csv"
    shell:
        """
        python3 ../code/run_HMM.py --freq {input.freq} --test {input.test} --out {output.out} --optim_out {output.optim_out}
        """

rule analyze_sim_results:
    message: "Analyzing simulation results."
    input:
        "output/inferred/inferred_lineages.json",
        "output/inferred/optimization_results.csv",
        "output/inferred/inferred_lineages_controls.json",
        "output/inferred/optimization_results_controls.csv",
    output:
        "output/results/breakpoint_distance_by_count.png",
        "output/results/sens_vs_hamming.png",
        "output/results/first_20_true_and_inferred.png",
        "output/results/breakpoint_confusion_matrix.csv"
    shell:
        """
        python3 analyze_sim_results.py
        """